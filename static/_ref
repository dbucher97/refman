#compdef ref

_ref() {
  local state
  local line

  _arguments -C \
    '-h[Display Help Message.]' \
    '1:cmd:->cmds' \
    '*:: :->args' \

  case "$state" in
    (cmds)
      local commands; commands=(
        'open:Open a PDF by BibTeX ID.'
        'info:Get Info for PDF.'
        'watch:Watch References directory for changes.'
        'start:Start Continuous observation.'
        'stop:Stop Continuous observation.'
        'fix:Jump to manual action in bib database.'
      )
      _describe -t commands 'command' commands
    ;;
    (args)

      case $line[1] in
        (info)
          _files -g "*.pdf"
        ;;
        (open)
          if [ -f "$HOME/.refman.conf" ]; then
            f=$(cut -d '=' -f 2 < "$HOME/.refman.conf" | tr -d '[:space:]')
            eval f=$f
          else
            f="$HOME/papers"
          fi
          f="$f/.bib_cache"
          local keys; keys=()
          if [ -f "$f" ]; then
            keys=("${(@f)$(< $f)}")
          fi
          _describe -t keys 'keys' keys
        ;;
      esac
    ;;
  esac
}

_ref
